varnishtest "Test basics"

server s1 {
    rxreq
    expect req.url == "/foo"
    expect req.http.X-Device == "desktop"
    txresp -hdr "Vary: X-Device"

    rxreq
    expect req.url == "/bar"
    expect req.http.X-Device == "desktop"
    txresp -hdr "Vary: X-Device"

    rxreq
    expect req.url == "/bar"
    expect req.http.X-Device == "mobile"
    txresp -hdr "Vary: X-Device"

    rxreq
    expect req.url == "/foo"
    expect req.http.X-Device == "desktop"
    txresp -hdr "Vary: X-Device"

    rxreq
    expect req.url == "/baz"
    expect req.http.X-Device == "mobile"
    txresp -hdr "Vary: X-Device"

    rxreq
    expect req.url == "/baz"
    expect req.http.X-Device == "mobile"
    txresp -hdr "Vary: X-Device"
} -start

varnish v1 -vcl+backend {
    import gossip from "${vmod_topbuild}/src/.libs/libvmod_gossip.so";

    sub vcl_recv {
        if (req.url ~ "^/gossip/dump/.+") {
            gossip.dump(regsub(req.url, "/gossip/dump", ""));
            return (synth(200, "Now dumping."));
        } elsif (req.url == "/gossip/discard/") {
            gossip.discard();
            return (synth(200, "Discarded."));
        }

        set req.http.X-ClientIp = client.ip;
    }

    sub vcl_backend_response {
        set beresp.http.X-Gossip-Info =
            {"{"} +
            {""path":""} + gossip.escape_json_string(bereq.url) + {"","} +
            {""device":""} + gossip.escape_json_string(bereq.http.X-Device) + {"","} +
            {""ip":""} + gossip.escape_json_string(bereq.http.X-ClientIp) + {"""} +
            {"}"};
        set beresp.ttl = 0.5s;
        set beresp.grace = 10s;
    }
} -start

client c1 {
    txreq -url /foo \
          -hdr "X-Device: desktop"
    rxresp
    expect resp.status == 200

    txreq -url /foo \
          -hdr "X-Device: desktop"
    rxresp
    expect resp.status == 200

    txreq -url /bar \
          -hdr "X-Device: desktop"
    rxresp
    expect resp.status == 200

    txreq -url /bar \
          -hdr "X-Device: mobile"
    rxresp
    expect resp.status == 200

    delay 1.0
    txreq -url /gossip/dump${tmpdir}/objects1.json
    rxresp
    expect resp.status == 200
    delay 0.1

    txreq -url /foo \
          -hdr "X-Device: desktop"
    rxresp
    expect resp.status == 200

    txreq -url /baz \
          -hdr "X-Device: mobile"
    rxresp
    expect resp.status == 200

    delay 1.0
    txreq -url /gossip/dump${tmpdir}/objects2.json
    rxresp
    expect resp.status == 200
    delay 0.1

    txreq -url /baz \
          -hdr "X-Device: mobile"
    rxresp
    expect resp.status == 200

    delay 1.0
    txreq -url /gossip/discard/
    rxresp
    expect resp.status == 200
    delay 0.1

    delay 1.0
    txreq -url /gossip/dump${tmpdir}/objects3.json
    rxresp
    expect resp.status == 200
    delay 0.1
} -run

delay 1.0

shell {
    cat <<'EOF' > ${tmpdir}/parse.sh
echo '----'
cat ${tmpdir}/$1 | sed -n 's/^.*"path":"\([^"]*\)".*$/\1/p' | sort
echo '----'
EOF
    chmod +x ${tmpdir}/parse.sh
}

shell -expect "----\n/bar\n/bar\n/foo\n----\n" {
    ${tmpdir}/parse.sh objects1.json
}

shell -expect "----\n/baz\n/foo\n----\n" {
    ${tmpdir}/parse.sh objects2.json
}

shell -expect "----\n----\n" {
    ${tmpdir}/parse.sh objects3.json
}

varnish v1 -expect client_req == 11

varnish v1 -expect MGT.child_panic == 0
